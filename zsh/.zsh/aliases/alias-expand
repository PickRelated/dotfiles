#!/usr/bin/env bash
# vim: filetype=zsh

globalias() {
  if [[ $LBUFFER =~ '[a-zA-Z0-9]+$' ]]; then
    zle _expand_alias
    zle expand-word
  fi
  zle self-insert
}
zle -N globalias

# immediately calls expanded alias
typeset -a call_aliases
call_aliases=()
call_alias() {
  alias $@
  args="$@"
  args=${args%%\=*}
  call_aliases+=(${args##* })
}

# prints output of fzf command
typeset -a print_aliases
print_aliases=()
print_alias() {
  alias $@
  args="$@"
  args=${args%%\=*}
  print_aliases+=(${args##* })
}

expand-alias() {
  [[ $LBUFFER =~ "(^${(j:$|^:)call_aliases}$)" ]]; is_call_alias=$?
  [[ $LBUFFER =~ "(^${(j:$|^:)print_aliases}$)" ]]; is_print_alias=$?
  if [[ "$is_call_alias" = "0" ]]; then
    zle globalias
    EXPANDED_COMMAND=$LBUFFER
    zle backward-kill-line
    eval $EXPANDED_COMMAND
    zle accept-line
  elif [[ "$is_print_alias" = "0" ]]; then
    zle globalias
    output=$(eval $LBUFFER)
    zle backward-kill-line
    zle accept-line
    print -z $output
  else
    alias_value=$(alias "$LBUFFER")
    zle globalias
    if [[ -n "$alias_value" ]]; then
      zle backward-delete-char
    fi
  fi
}
zle -N expand-alias
