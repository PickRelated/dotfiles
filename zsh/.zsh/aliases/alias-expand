#!/usr/bin/env bash
# vim: filetype=zsh

globalias() {
  if [[ $LBUFFER =~ '[a-zA-Z0-9]+$' ]]; then
    zle _expand_alias
    zle expand-word
  fi
  zle self-insert
}
zle -N globalias

# triggered by whitespace
typeset -a call_aliases
call_aliases=()
call_alias() {
  alias $@
  args="$@"
  args=${args%%\=*}
  call_aliases+=(${args##* })
}

# triggered by whitespace. performs print -z
typeset -a print_aliases
print_aliases=()
print_alias() {
  alias $@
  args="$@"
  args=${args%%\=*}
  print_aliases+=(${args##* })
}

expand-alias() {
  [[ $LBUFFER =~ "(^${(j:$|^:)call_aliases}$)" ]]; is_quick_alias=$?
  [[ $LBUFFER =~ "(^${(j:$|^:)print_aliases}$)" ]]; is_print_alias=$?
  if [[ "$is_quick_alias" = "0" ]]; then
    zle globalias
    EXPANDED_COMMAND=$LBUFFER
    zle backward-kill-line
    eval $EXPANDED_COMMAND
    zle accept-line
  elif [[ "$is_print_alias" = "0" ]]; then
    zle globalias
    output=$(eval $LBUFFER)
    zle backward-kill-line
    zle accept-line
    print -z $output
  else
    zle globalias
  fi
}
zle -N expand-alias
